         EJECT ,
***********************************************************************
* THIS CODE WAS COBBLED UP FROM VARIOUS EXISTING PROGRAMS (SEE FILE   *
* 172 OF THE CBT TAPE) IN ORDER TO TEST WHETHER AN ENQUEUE EXISTS     *
* AGAINST THE DATASET UNDER THE SYSDSN MAJOR NAME. IN OTHER WORDS,    *
* IS THE FILE IN USE?                                                 *
*                                                                     *
* WRITTEN BY DAVID CARTWRIGHT AT AGCO UK LTD AUGUST 2001              *
*                                                                     *
* 29AUG01 DHC COUNT NO CATLG ENTRIES AS ERROR, NOT 0C4                *
*                                                                     *
* 18MAY10 SKIP CLUSTER NAME CATALOG SEARCH FOR ICF CATALOGS   DRK MAY10
*         AND SYS1.VVDS.VVOLUME DATA SETS                     DRK MAY10
*                                                                     *
***********************************************************************
         SPACE 1
         CLI   ENQK+1,0       SHOULD WE CHECK FOR USERS?
         BE    ENQEND         NO, SKIP PAST IT
         SR    R4,R4               CLEAR A FULLWORD
         ST    R4,GQTOKEN          RESET TOKEN POINTER
         LA    R4,DS1DSNAM         POINT TO THE DSNAME
         LH    R6,DSNLEN           GET LENGTH OF NAME
         CLC   VTFDSORG,=CL3'VS '  IS IT VSAM?
         BNE   ENQFIND             NO, GO PROCESS IT
         TM    DS1OPTCD,DS1OPTBC   ICF CATALOG?               DRK MAY10
         BO    ENQFIND             YES, GO PROCESS IT         DRK MAY10
         CLC   DS1DSNAM(9),=CL9'SYS1.VVDS'  VVDS?             DRK MAY10
         BE    ENQFIND             YES, GO PROCESS IT         DRK MAY10
         EJECT ,
***********************************************************************
*                                                                     *
*        FIND DATA SETS ASSOCIATED WITH THIS ENTITY IN ORDER TO       *
*        FIND THE CLUSTER NAME FOR VSAM DATASETS.                     *
*                                                                     *
*        CALL SVC 26 TO RETURN CLUSTER ASSOCIATIONS                   *
*                                                                     *
***********************************************************************
         SPACE 1
         XC    CTGPL2,CTGPL2       ZERO PARM LIST
         MVI   CTGPL2,B'00000110'  SET CTGOPTN1
         MVI   CTGPL2+1,B'01000001' SET CTGOPTN2
         MVI   CTGPL2+2,B'10000001' SET CTGOPTN3
         LA    R1,DS1DSNAM         LOAD ADDRESS OF NAME
         ST    R1,CTGPL2+4         SET CTGENT
         L     R0,SS26WRK          LOAD LENGTH OF WORK AREA
         L     R15,AS26WRK         LOAD ADDRESS OF SVC26 WORK AREA
         STH   R0,0(R15)           SAVE LENGTH AT START OF WORK AREA
         ST    R15,CTGPL2+12       SET CTGWKA
         MVI   CTGPL2+16,B'00100001' SET CTGOPTNS
         LA    R15,OPTNUM          LOAD NUMBER OF OPTIONS
         STC   R15,CTGPL2+19       SET CTGNOFLD
         LA    R14,OPTNLIST        LOAD ADDRESS OF OPTION LIST
         LA    R1,CTGPL2+28        SAVE IN CTGFIELD
         LA    R2,OPT1             LOAD ADDRESS OF OPTION NAME LIST
OPTLOOP  XC    0(24,R14),0(R14)    ZERO THE ENTRY
         MVI   0(R14),X'01'        SET FIRST FIELD
         ST    R2,8(R14)           SET ADDRESS OF OPTION NAME
         ST    R14,0(R1)           SAVE OPTNLIST ADDRESS IN CTGPL
         LA    R1,4(R1)            POINT TO NEXT OPTNLIST ADDRESS
         LA    R14,24(R14)         POINT TO NEXT ENTRY IN OPTION LIST
         LA    R2,8(R2)            POINT TO NEXT OPTION NAME
         BCT   R15,OPTLOOP         LOOP
         SPACE 3
DOS26    LA    R1,CTGPL2           LOAD ADDRESS OF WORK AREA
         SVC   26                  CALL CATALOG SEARCH FOR ENTITY
         SPACE 1
         LTR   R15,R15             TEST RETURN
         BZ    OPTPROC             GOOD, PROCESS OPTION LIST
         CLI   CATK+1,0            HAVE WE ALREADY CHECKED THE CATALOG?
         BE    OPTERR              NO, SO IT'S AN ERROR
         CLI   VTFCATLG,C'N'       DID WE FIND IT UNCAT
         BNE   OPTERR              NO, SO SUPERLOCATE ERROR
         MVI   VTFENQ,C'N'         UNCATALOGED CLUSTER CANNOT BE IN USE
         B     ENQEND              SKIP ERROR MESSAGE
OPTERR   DS    0H                  PROBLEM IN SUPERLOCATE CODE
*        MVI   VTFENQ,C'?'         SHOW WE CANNOT FIND ENQ STATUS
         MVI   VTFENQ,C'N'         UNCATALOGED CLUSTER CANNOT BE IN USE
         VTOCMSG MSGCAT            ISSUE A MESSAGE
         B     ENQEND              THEN GIVE UP
         SPACE 2
OPTPROC  DS    0H                  FIND CLUSTER NAME
         ICM   R4,B'1111',OPTNLIST+20 LOAD ADDRESS OF ENTRY TYPE
         BZ    OPTERR              NONE, ERROR
         CLI   0(R4),C'C'          CLUSTER NAME SAME AS ENTRY?
         BE    GETCLASS            YES, USE THAT NAME
         ICM   R4,B'1111',OPTNLIST+68  GET LIST POINTER
         L     R15,OPTNLIST+64     GET LENGTH OF LIST
         SR    R14,R14             CLEAR EVEN REGISTER
         D     R14,=F'45'          DIVIDE BY LENGTH OF EACH ENTRY
         LTR   R6,R15              USE COUNT OF ENTRIES
         BZ    OPTERR              NONE, ERROR
GETCLLUP DS    0H                  LOOP THROUGH LIST
         CLI   0(R4),C'C'          TEST FOR CLUSTER
         BE    GETCLASS            FOUND THE CLUSTER
         LA    R4,45(,R4)          ELSE INCREMENT THE POINTER
         BCT   R6,GETCLLUP         AND GO ROUND AGAIN
         BZ    OPTERR              NONE, ERROR
         SPACE 1
GETCLASS DS    0H                  FOUND CLUSTER, SEE IF IT IS IN USE
         LA    R4,1(R4)            POINT AT CLUSTERNAME
         LR    R1,R4               COPY THAT ADDRESS
         SR    R6,R6               CLEAR COUNTER
         LA    R2,44               SET MAXIMUM LENGTH
         SPACE 1
LENGTH1  DS    0H                  EXAMINE EACH BYTE
         CLI   0(R1),X'40'         IS IT BLANK?
         BE    LENGTH2             YES, IS LAST CHARACTER
         LA    R1,1(,R1)           POINT AT NEXT CHARACTER
         LA    R6,1(,R6)           INCREMENT COUNTER
         BCT   R2,LENGTH1          LOOP ROUND
         SPACE 1
LENGTH2  DS    0H                  FOUND LENGTH OF DSNAME
         EJECT ,
*
*        FORMAT ENQUEUE STATUS FOR MAJOR=SYSDSN
*
ENQFIND  DS    0H                  TEST FOR DATASET IN USE
         LA    R2,ENQWORK          POINT TO WORK AREA
         GQSCAN AREA=((2),L'ENQWORK),SCOPE=ALL,TOKEN=GQTOKEN,          X
               REQLIM=MAX,RESNAME=(RESNAME,(4),(6)),MF=(E,ENQSCAN)
         LH    R15,GQTAB(R15)       GET OFFSET VECTOR FOR RETURN CODE
         B     QRETURN(R15)        USE VECTOR TABLE
QRETURN  B     ENQTEST             SOMEONE OWNS IT
         B     ENQNOT              NO-ONE OWNS IT
         B     ENQERR              GQSCAN ERROR
         B     ENQERR              GQSCAN ERROR
         SPACE 1
ENQERR   DS    0H                  SOMETHING WRONG WITH GQSCAN
         MVI   VTFENQ,C'*'         FLAG EXCEPTIONAL CONDITION
         VTOCMSG MSGENQ            ISSUE A MESSAGE
         B     ENQEND              THEN GIVE UP
         SPACE 1
ENQNOT   DS    0H                  NOBODY HAS ENQUEUED THE DATASET
         MVI   VTFENQ,C'N'         SHOW THAT
         B     ENQEND              THEN CARRY ON
         SPACE 1
ENQTEST  DS    0H                  SOMEBODY HAS ENQUEUED THE DATASET
         MVI   VTFENQ,C'S'         ASSUME IT IS SHARED
         ST    R0,LENGTHS          SAVE LENGTH OF RIB AND RIBE
         LA    R2,ENQWORK          POINT TO WORK AREA
         USING RIB,R2              MAP RIB AT THE START OF WORK AREA
         SPACE 1
RIBLOOP  DS    0H                  CHECK EACH RIB
         LH    R4,LENRIB           GET LENGTH OF RIB
         AR    R4,R2               POINT PAST RIB
         AH    R4,RIBVLEN          POINT PAST VARIABLE AREA
         USING RIBE,R4             ESTABLISH RIBE ADDRESSABILITY
         ICM   R14,B'1111',RIBNRIBE DO WE HAVE A RIBE?
         BZ    ENQERR              NO, WRONG
         SPACE 1
RIBELOOP DS    0H                  TEST FOR EXCLUSIVE ENQUEUE
         TM    RIBERFLG,RIBETYPE   TEST FOR EXCLUSIVE ENQ
         BNO   ENQEXCL             YES, GO SHOW IT
         AH    R4,LENRIBE          INCREMENT OVER THIS RIBE
         BCT   R14,RIBELOOP        GO TEST NEXT ENQUEUE
ENQSHR   DS    0H                  OWNED NON - EXCLUSIVELY
         LR    R2,R4               NEXT RIB FOLLOWS ON RIBES
         BCT   R1,RIBLOOP          TEST ALL RIBS
         B     ENQEND              THEN CARRY ON
         SPACE 1
ENQEXCL  DS    0H                  OWNED EXCLUSIVELY
         MVI   VTFENQ,C'E'         SHOW THAT
         B     ENQEND              THEN CARRY ON, ONE IS ENOUGH
         EJECT ,
***********************************************************************
*        SUBROUTINE TO MANAGE STORAGE                                 *
*              ON ENTRY R0 = SP AND SIZE REQUIRED                     *
*              ON EXIT  R1 = AREA ADDRESS                             *
***********************************************************************
         SPACE 1
GETSTORE DS    0H                  SUBROUTINE ENTRY
         GETMAIN R,LV=(0),LOC=BELOW ASK FOR 24 BIT STORAGE
         SPACE 1
         BR    R4                  RETURN TO CALLER
         SPACE 2
FREESTOR DS    0H                  SUBROUTINE ENTRY
         FREEMAIN R,LV=(0),A=(1)   RELEASE PREVIOUSLY ACQUIRED STORAGE
         SPACE 1
         BR    R4                  RETURN TO CALLER
         SPACE 3
***********************************************************************
*        SUBROUTINE TO CLEAR STORAGE                                  *
*        ON ENTRY:                                                    *
*              R0   =  LENGTH OF STORAGE ELEMENT                      *
*              R1   -> ADDRESS OF STORAGE ELEMENT                     *
***********************************************************************
         SPACE 2
CLEAR    DS    0H                  SUBROUTINE ENTRY
         SR    R3,R3               SET OPERAND1 TO ZERO
         LA    R2,0(R1)            SET ADDRESS TO CLEAR
         LR    R3,R0               SET LENGTH
         SR    R4,R4               SET FROM ADDRESS ZERO
         SR    R5,R5               PAD TO ZERO FROM ZERO LENGTH
         MVCL  R2,R4               CLEAR THE STORAGE
         SPACE 2
CLEARED  DS    0H                  END OF SUBROUTINE
         BR    R14                 RETURN TO CALLER
         SPACE 3
*
*        VECTOR TABLE TO SELECT GQSCAN RETURN  CODE PROCESS
*
GQTAB    DC    AL2(0)              RC=X'00' -> PROCESS RIB
         DC    AL2(12)             FILLER   -> INVALID ENTRY
         DC    AL2(4)                   04     NO ENQUEUES
         DC    AL2(12)             FILLER   -> INVALID ENTRY
         DC    AL2(0)                   08     INCOMPLETE DATA
         DC    AL2(12)                  0A     GQSCAN ERROR
         DC    AL2(12)             FILLER   -> INVALID ENTRY
         DC    AL2(8)                   0C     GQSCAN ERROR
         DC    AL2(12)             FILLER   -> INVALID ENTRY
         DC    AL2(8)                   10     GQSCAN ERROR
         DC    AL2(12)             FILLER   -> INVALID ENTRY
         DC    AL2(8)                   14     GQSCAN ERROR
         SPACE 3
MSGCAT   MSGZ  'SUPERLOCATE ERROR'
MSGENQ   MSGZ  'ERROR IN ENQ DETECTION'
         SPACE 1
ENQEND   DS    0H
         DROP  R2                  END RIB  ADDRESSABILITY
         DROP  R4                  END RIBE ADDRESSABILITY
         EJECT ,
